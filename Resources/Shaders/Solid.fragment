/*
	Copyright 2020 Myles Trevino
	Licensed under the Apache License, Version 2.0
	https://www.apache.org/licenses/LICENSE-2.0
*/


#version 330 core

in vec4 fragment_position_light_space;

uniform vec3 color;
uniform float shadow_intensity;
uniform sampler2D shadow_map;

out vec4 output_color;


float calculate_shadow()
{
	vec3 projection = (fragment_position_light_space.xyz/
		fragment_position_light_space.w)*.5f+.5f;

	float current_depth = projection.z;
	const float bias = .0005f;
	vec2 texel_size = 1.f/textureSize(shadow_map, 0);
	float shadow = 0.f;

	for(int x = -1; x <= 1; ++x)
	{
		for(int y = -1; y <= 1; ++y)
		{
			float depth = texture(shadow_map, projection.xy+vec2(x, y)*texel_size).r;
			shadow += current_depth-bias > depth ? 1.f-shadow_intensity : 1.f;
		}
	}

	return shadow/9.f;
}


void main()
{
	output_color = vec4(calculate_shadow()*color, 1.f);
}